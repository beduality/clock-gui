plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
    id "io.freefair.lombok" version "8.6"
}

group = "io.github.beduality"

repositories {
    mavenCentral()
    maven {
        url "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = "https://repo.aikar.co/content/groups/aikar/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.6-R0.1-SNAPSHOT"
    implementation "co.aikar:acf-paper:0.5.0-SNAPSHOT"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

bukkit {
    name = "ClockGUI"
    version = project.version
    main = "io.github.beduality.clockgui.ClockGuiPlugin"
    description = "Adds a 12-hour display to Minecraft's clock with a quick-click GUI!"
    apiVersion = "1.20"
    author = "Block-Entity Duality Team"
    website = "https://github.com/beduality/clock-gui"
}

shadowJar {
   archiveBaseName.set('ClockGUI')
   archiveClassifier.set('')
   archiveVersion.set(project.version)
}

task cleanPlugins() {
    delete fileTree(dir: "../server/data/plugins", include: "ClockGUI*.jar")
}

task buildmv(dependsOn: ['clean', 'shadowJar', 'cleanPlugins'], type: Copy) {
    from "${buildDir}/libs"
    into "../server/data/plugins"
}
